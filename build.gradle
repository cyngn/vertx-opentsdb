apply plugin: 'java'
// Uncomment below when doing official builds
//apply from: "gradle/sona.gradle"

version = '0.7.0'
group = "com.cyngn.vertx"
archivesBaseName = "vertx-opentsdb"

if (!JavaVersion.current().java8Compatible) {
    throw new IllegalStateException('''A Haiku:
                                      |  This needs Java 8,
                                      |  You are using something else,
                                      |  Refresh. Try again.'''.stripMargin())
}

repositories {
    mavenCentral()
    maven {
        url = 'http://oss.sonatype.org/content/repositories/snapshots/'
    }
}

dependencies {
    compile 'io.vertx:vertx-core:3.1.0'
    compile "joda-time:joda-time:2.4"
    compile "com.google.guava:guava:18.0"
    compile "org.apache.commons:commons-lang3:3.4"
    compile "io.vertx:vertx-codegen:3.1.0"
    testCompile "junit:junit:4.11"
    testCompile "io.vertx:vertx-unit:3.1.0"
    testCompile "io.vertx:vertx-web:3.1.0"
    testCompile 'io.vertx:vertx-web:3.1.0:tests'
    testCompile 'io.vertx:vertx-core:3.1.0:tests'
    testCompile "org.mockito:mockito-core:1.9.5"
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}

task release() << {}

gradle.taskGraph.whenReady {taskGraph ->
    if (!taskGraph.hasTask(release)) {
        version += '-SNAPSHOT'
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}
